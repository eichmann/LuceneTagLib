package edu.uiowa.lucene.biomedical;

import java.util.*;
import org.apache.lucene.analysis.standard.StandardAnalyzer;
import org.apache.lucene.index.Term;
import org.apache.lucene.search.Query;
import org.apache.lucene.search.TermQuery;
import org.apache.lucene.search.BooleanClause;
import org.apache.lucene.search.BooleanQuery;
import org.apache.lucene.queryparser.classic.QueryParser;
import org.apache.commons.logging.Log;
import org.apache.commons.logging.LogFactory;

parser code {:
    static final Log log =LogFactory.getLog(BiomedicalParseCup.class);

        public static void main(String args[]) throws Exception {
                new BiomedicalParseCup(new biomedicalParseFlex(System.in)).parse();
        }
:}

terminal LPAREN, RPAREN;
terminal String, AND_OP, OR_OP, NOT_OP;

non terminal Query	top, Pat;
non terminal Vector<String> TokenList;

precedence left OR_OP;
precedence left AND_OP;
precedence left NOT_OP;

start with top;

top ::=	Pat:p		{: RESULT = p;
					   BiomedicalParseCup.log.info("\tquery top: " + RESULT);
					 :}
	;

Pat ::= 
	| LPAREN Pat:p RPAREN		{: 	RESULT = p;
									BiomedicalParseCup.log.info("\tgrouped query: " + RESULT);
								:}
	| TokenList:p	{:	Query theQuery = new BooleanQuery();
						for (String e : p) {
							BiomedicalParseCup.log.info("\tterm: " + e);
							QueryParser theQueryParser = new QueryParser(org.apache.lucene.util.Version.LUCENE_30, "content", new BiomedicalNormalizer());
							Query termQuery = theQueryParser.parse(e);            	
							((BooleanQuery)theQuery).add(termQuery,BooleanClause.Occur.MUST);
						}
						RESULT = theQuery;
						BiomedicalParseCup.log.info("\ttoken string: " + p + "\t" + RESULT);
					:}
	| Pat:leftPat AND_OP Pat:rightPat	{:	if (rightPat == null) {
												RESULT = leftPat;
											} else if (leftPat == null) {
												RESULT = rightPat;
											} else {
												Query theQuery = new BooleanQuery();
												((BooleanQuery)theQuery).add(leftPat, BooleanClause.Occur.MUST);
												((BooleanQuery)theQuery).add(rightPat, BooleanClause.Occur.MUST);
												RESULT = theQuery;
												BiomedicalParseCup.log.info("\tand query: " + RESULT);
											}
										:}
	| Pat:leftPat OR_OP Pat:rightPat	{:	if (rightPat != null) {
												Query theQuery = new BooleanQuery();
												((BooleanQuery)theQuery).add(leftPat, BooleanClause.Occur.SHOULD);
												((BooleanQuery)theQuery).add(rightPat, BooleanClause.Occur.SHOULD);
												RESULT = theQuery;
												BiomedicalParseCup.log.info("\tor query: " + RESULT);
											} else
												RESULT = leftPat;
										:}
	| NOT_OP Pat:p	{:	Query theQuery = new BooleanQuery();
						((BooleanQuery)theQuery).add(p, BooleanClause.Occur.MUST_NOT);
						RESULT = theQuery;
						BiomedicalParseCup.log.info("\tnot query: " + RESULT);
					:}
	 ;
	
TokenList ::= TokenList:p1  String:p2	{:	p1.addElement((String)p2);
									   		RESULT = p1;
									   		BiomedicalParseCup.log.info("\ttoken list: " + RESULT);
	 								 	:}
	 		|   String:p				{:	Vector<String> list = new Vector<String>();
	 								   		list.addElement((String)p);
	 								   		RESULT = list;
									   		BiomedicalParseCup.log.info("\ttoken list first token: " + RESULT);
	 								 	:}
		 ;
	